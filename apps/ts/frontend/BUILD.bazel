load("@aspect_rules_js//js:defs.bzl", "js_library", "js_run_binary", "js_run_devserver")
load("@aspect_rules_ts//ts:defs.bzl", "ts_config")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//:tsconfig-to-swcconfig/package_json.bzl", tsconfig_to_swcconfig = "bin")
load("@npm//apps/ts/frontend:vite/package_json.bzl", "bin")
load("@npm//apps/ts/frontend:vitest/package_json.bzl", vitest = "bin")

npm_link_all_packages(name = "node_modules")

exports_files(
    [
        "tsconfig.json",
        "vite-env.d.ts",
    ],
    visibility = ["//apps/ts/frontend:__subpackages__"],
)

tsconfig_deps = [
    "//apps/ts/frontend:tsconfig.json",
    "//apps/ts/frontend:vite-env.d.ts",
    "//:tsconfig_base",
    "//:tsconfig_strictest",
]

ts_config(
    name = "app_tsconfig",
    src = "tsconfig.app.json",
    visibility = ["//apps/ts/frontend:__subpackages__"],
    deps = tsconfig_deps,
)

js_library(
    name = "vite.config",
    srcs = ["vite.config.ts"],
    visibility = ["//apps/ts/frontend:__subpackages__"],
    deps = [
        ":node_modules/@vitejs/plugin-react",
        ":node_modules/vite-plugin-svgr",
        ":node_modules/vitest",
    ],
)

RUNTIME_DEPS = [
    "//apps/ts/frontend/src:assets",
    "//apps/ts/frontend/src",
    "//apps/ts/frontend/public",
    "index.html",
    "package.json",
    "//apps/ts/frontend:app_tsconfig",
    ":node_modules/react-dom",
    ":node_modules/react",
    ":node_modules/web-vitals",
    ":vite.config",
]

tsconfig_to_swcconfig.t2s(
    name = "write_swcrc",
    srcs = [
        "tsconfig.app.json",
        "tsconfig.json",
        "//:tsconfig_base",
        "//:tsconfig_strictest",
    ],
    args = [
        "--filename",
        "$(location tsconfig.app.json)",
    ],
    stdout = ".swcrc",
    visibility = ["//apps/ts/frontend:__subpackages__"],
)

js_library(
    name = "package_json",
    srcs = ["package.json"],
    visibility = ["//visibility:public"],
    deps = [":node_modules/eslint-config-react-app"],
)

bin.vite_binary(
    name = "vite",
    chdir = package_name(),
    data = ["vite.config"],
)

# Fast developer round-trip under ibazel
js_run_devserver(
    name = "start",
    args = ["."],
    data = RUNTIME_DEPS,
    tool = ":vite",
)

# Create production release artifacts
js_run_binary(
    name = "build",
    srcs = RUNTIME_DEPS,
    args = ["build"],
    mnemonic = "ViteBuild",
    out_dirs = ["dist"],
    tool = ":vite",
)

# Hosts the production-bundled application in a web server
bin.vite_binary(
    name = "preview",
    args = ["preview"],
    chdir = package_name(),
    data = [":build"],
)

# ====================================== Tests setup ==============================================
ts_config(
    name = "spec_tsconfig",
    src = "tsconfig.spec.json",
    visibility = ["//apps/ts/frontend:__subpackages__"],
    deps = tsconfig_deps,
)

tsconfig_to_swcconfig.t2s(
    name = "write_spec_swcrc",
    srcs = [
        "tsconfig.json",
        "tsconfig.spec.json",
        "//:tsconfig_base",
        "//:tsconfig_strictest",
    ],
    args = [
        "--filename",
        "$(location tsconfig.spec.json)",
    ],
    stdout = ".swcrc_spec",
    visibility = ["//apps/ts/frontend:__subpackages__"],
)

vitest.vitest_test(
    name = "test",
    # Perform a single run without watch mode. If we want to watch we will use ibazel.
    args = ["run"],
    # Paths in the configuration file are relative to its folder, so we must use that as the
    # working directory since vite doesn't handle this itself.
    chdir = package_name(),
    data = [
        "//apps/ts/frontend/src:test_lib",
    ] + RUNTIME_DEPS,
)
